apply plugin: 'base'

buildscript {
    ext {
        lightVersion = '0.0.1-SNAPSHOT'
        javaVersion = '1.6'
        slf4jVersion = '1.7.21'
        log4jVersion = '2.6'
        jodaTimeVersion = '2.6'
        junitVersion = '4.12'
        hamcrestVersion = '1.3'
        mockitoVersion = '1.10.19'
        powerMockVersion = '1.6.5'
    }

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
//        maven {
//            url "https://plugins.gradle.org/"
//        }
    }

    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
    }
}

allprojects {
    apply plugin: 'jacoco'
    apply plugin: "com.github.kt3k.coveralls"

    group = 'com.geeksaga.light'

    repositories {
        jcenter()
    }

    jacoco {
        toolVersion = '0.7.1.201405082137'
    }
}

version = lightVersion

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        mavenLocal()
        jcenter()
        maven { url "https://repo.spring.io/libs-release" }
        maven { url "https://repository.jboss.org/nexus/content/repositories/releases" }
        maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
        flatDir {
            dirs 'libs'
            //'$rootProject.projectDir/libs'
        }
    }

    dependencies {
        compile "org.slf4j:slf4j-api:$slf4jVersion"
        compile "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
        compile "org.apache.logging.log4j:log4j-api:$log4jVersion"
        compile "org.apache.logging.log4j:log4j-core:$log4jVersion"
        // compile "joda-time:joda-time:$jodaTimeVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.hamcrest:hamcrest-all:$hamcrestVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
        testCompile "org.powermock:powermock-module-junit4:$powerMockVersion"
        testCompile "org.powermock:powermock-api-mockito:$powerMockVersion"
    }

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    version = lightVersion

    jar {
        manifest.attributes provider: 'geeksaga'
    }

    test {
        filter {
            includeTestsMatching "*Test"
        }
    }

    jacocoTestReport {
        reports {
            html.enabled = true
            xml.enabled = true
            csv.enabled = false
        }
    }

    task copyToLibrary(type: Copy) {
        from configurations.runtime
        into "${rootProject.rootDir}/install/libs"

        exclude 'light.**.jar'
        exclude 'tools.jar'
    }

    jar.finalizedBy(project.tasks.copyToLibrary)
}

project(':agent') {
    dependencies {
        compile project(':agent.core')
    }
}

project(':agent.core') {
    dependencies {
        compile project(':common')
    }
}

project(':agent.tools') {
    dependencies {
        compile project(':agent')
    }
}

project(':profiler') {
    dependencies {
        compile project(':agent')
        compile project(':agent.core')
        compile project(':common')
        compile project(':repository')
    }
}

project(':repository') {
    dependencies {
        compile project(':agent.core')
        compile project(':common')

        testCompile project(':profiler')
    }
}

project(':server.jetty') {
    dependencies {
        compile project(':repository')
    }
}

project(':server.tomcat') {
    dependencies {
        compile project(':repository')
    }
}

task jacocoRootReport(type: JacocoReport) {
    dependsOn = subprojects.test
    sourceDirectories = files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(subprojects.sourceSets.main.output.classesDir)
    executionData = files(subprojects.jacocoTestReport.executionData)
    reports {
        html.enabled = true
        xml.enabled = true
        xml.destination = "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
    }

    setOnlyIf { true }
}

coveralls {
    sourceDirs = files(subprojects.sourceSets.main.allSource.srcDirs).files.absolutePath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}